# Three-generated arrangements and freeness of continuous splines

## Loading packages
You can ensure that you have all necessary packages and functions for this tutorial by uploading the file "Scripts_for_splines_on_hyperplanes.m2" and runing it.  Alternatively, here are the functions you'll need:

```
loadPackage("HyperplaneArrangements",Reload=>true);
loadPackage("AlgebraicSplines",Reload=>true);
```

```
--Inputs: A: a hyperplane arrangement A
--Outputs: A sequence (E,I) where
--        E records the edges between vertices of Z(A)
--        I records the edge labels using the smoothness distribution r

zonotopeEdgeLabels = method()
zonotopeEdgeLabels = A ->(
    S := ring A;--get the ring of A
    hyps := A#(first keys A); --get the list of hyperplanes definining A
    M := coefficients A; --get the coefficient matrix of A
    normals := entries transpose M;
    Z := sum apply(numcols M,j->convexHull((-M_{j})|M_{j}));--get the zonotope Z(A)
    V := entries transpose vertices Z; --get vertices of Z(A)
    --the remaining code inductively builds the list of edges and edge labels
    --by cycling through the vertices of Z(A) as recorded as columns of the matrix "vertices Z"
    --and adding the vector 2v for each vector in 'normals'.  If the result is a vertex of Z(A),
    --then the corresponding edge (recorded as a pair {i,j} of column indices of the matrix "vertices Z")
    --is added to E and the corresponding edge label is added to I.
    E := {};
    I := {};
    vtxcnt := 0;
    while vtxcnt<(#V) do(
	nrmcnt := 0;
	vtx := V_(vtxcnt);
	while nrmcnt<#normals do(
	    nrm := 2*normals_nrmcnt;
	    vtxc := vtx+nrm;
	    p := position(V,v->v==vtxc);
	    if not p===null then(
		E = append(E,{vtxcnt,p});
		I = append(I,hyps_(nrmcnt));
		);
	    nrmcnt=nrmcnt+1;
	    );
	vtxcnt = vtxcnt+1
	);
    (E,I,Z)
    )
```

## Freeness of continuous splines on the chambers of a three-dimensional hyperplane arrangement
In the file "Notes on presenting the non-trivial homology module for splines on fans in three dimensions" there is a proof sketched that the module of continuous splines on the chambers of a hyperplane arrangement is free if and only if the hyperplane arrangement is 3-generated.  The purpose of this tutorial is to explain what this means and see it in action using the code developed in these tutorials.

## Three-generated arrangements
We say a hyperplane arrangement is 'three-generated' if the relations among the linear forms defining the hyperplanes can be generated by relations of length three.  This is purely linear algebra.  (In the hyperplane arrangements literature, such an arrangement is also called *formal*, but we avoid this terminology because it clashes too much with a very different notion of *formal* in topology.)

```
S=QQ[x,y,z]
A=arrangement{x,y,z,x-y,x-z,y-z}
M=coefficients(A)
ker M
```
Observe that the given generators of the kernel are indeed all length three, so the arrangement A above is three-generated.

```
S=QQ[x,y,z]
A=arrangement{x,y,z,x+y+z}
M=coefficients(A)
ker M
```
Observe that there is a single relation, and it does not have length three.  So the arrangement A is not three-generated.

## How to determine if an arrangement is three-generated
It is not difficult to determine if an arrangement is three-generated, but it is not completely straightforward.  If the kernel of the coefficient matrix (that is, the space of all relations on the linear forms) happens to be generated by relations of length three, then you are lucky (as we were earlier).  However, if you happen to find generators of the kernel of the coefficient matrix and some of them do not have length three, it could just mean that you chose the 'wrong' set of generators.

Here is a way to determine if a three-dimensional central arrangement is three-generated.  There is a relation of length three between three linear forms if and only if the corresponding hyperplanes all meet along a line.  Thus to collect all relations of length three, we can call 'flats(2,A)' to get the codimension two flats.

Then we collect the relations among linear forms defining each codimension two flat, and regard this as a sub-space of the space of all relations among the linear forms (this part is annoying, as you have to put a coefficient of zero on all linear forms not involved in the relation coming from the flat).  Here is a function that does all this, and returns true if the arrangement is three generated and false otherwise.

```
isThreeGenerated = method()
isThreeGenerated = A -> (
    R := ring A;
    K := coefficientRing(R);
    M := coefficients A;
    RELS = ker M;
    ncM := numcols M;
    nrM := numrows M;
    FL2 := apply(flats(2,A),f->toList f);--get codimension two flats, turn into lists
    FL2trip := select(FL2,f->#f>=3);
    --If there are no flats of codimension two, then there are no relations of length three.
    if #FL2trip == 0 then(
	if (rank RELS)>0 then(
	    return false
	    )else(
	    return true
	    ));
    --The next function gets the length three relations as a submodule of all relations
    LCLRELS := sum apply(FL2trip, f->(
	    fsz := #f;
	    idsz := ncM-fsz;
	    c := 0;
	    i := 0;
	    ID := id_(K^(ncM-fsz));
	    relMat := {};
	    while i+c<ncM do(
		if isMember(i+c,f) then(
		    relMat = append(relMat,join(flatten entries(M_(i+c)),apply(idsz,i->0)));
		    c = c+1
		    )else(
		    relMat = append(relMat,join(apply(nrM,i->0),flatten entries(ID_i)));
		    i = i+1
		    )
		);
	    ker transpose matrix relMat
	    ));
    QUOT := prune(RELS/LCLRELS);
    if (rank QUOT)>0 then return false else true
    )
```
Test the function on the two we have already seen.
```
S=QQ[x,y,z]
A1=arrangement{x,y,z,x-y,x-z,y-z}
A2=arrangement{x,y,z,x+y+z}
isThreeGenerated A1
isThreeGenerated A2
```

According to the theorem whose proof sketch is in the notes, the continuous splines on the chambers of A1 should be free, while the continuous splines on the chambers of A2 should not be free.  Let's test.

```
(E1,I1,Z1)=zonotopeEdgeLabels(A1);
M1=generalizedSplines(E1,I1);
prune M1 --this should be free
```
```
(E2,I2,Z2)=zonotopeEdgeLabels(A2);
M2=generalizedSplines(E2,I2);
prune M2 --this should not be free
```

## Ziegler's pair
Here is a pair of hyperplane arrangements that are known as *Zielger's pair* in the theory of hyperplane arrangements.  We can construct them as follows.  Take six points, three on the $x$-axis and three on the $y$-axis (in projective two-space).  Regarding these six as a complete bipartite $K_{3,3}$ graph, get the nine lines that connect every point on the $x$-axis to every point on the $y$-axis.  This will be the arrangement $A_1$ (regard this in three dimensions by homogenizing).  Now get the arrangement $A_2$ by moving one of the points off the $y$-axis.

```
V1={{0,-3},{0,4},{0,7},{-3,0},{1,0},{17,0}}--on orthogonal lines
V2={{0,-3},{0,4},{0,7},{-3,0},{1,0},{17,1}}--not on orthogonal lines
PRS={{0,3},{0,4},{0,5},{1,3},{1,4},{1,5},{2,3},{2,4},{2,5}}--the pairs of points to connect
S=QQ[x,y,z]
L1=formsList(V1,PRS,0,BaseRing=>S)
L2=formsList(V2,PRS,0,BaseRing=>S)
A1=arrangement L1
A2=arrangement L2
```
It takes quite a while to compute the corresponding zonotopes and edge labels (there are 62 chambers and 108 edges).  The code may need optimizing!

```
time (E1,I1,Z1)=zonotopeEdgeLabels(A1);--long time
time (E2,I2,Z2)=zonotopeEdgeLabels(A2);--long time
```
The zonotopes are actually the same!  We can check that E1 and E2 are equal.
```
E1==E2
```
Moreover we can check that their two-dimensional faces are the same.
```
FS1=apply(faces(1,Z1),f->first f);
FS2=apply(faces(1,Z2),f->first f);
(sort FS1)==(sort FS2)
```
This means $A_1$ and $A_2$ define the same *oriented matroid*.  However, $A_1$ is not three-generated while $A_2$ is three-generated.
```
isThreeGenerated(A1)
isThreeGenerated(A2)
```
And we can check that the module of continuous splines on the chambers of $A_1$ is not free, while the module of continuous splines on the chambers of $A_2$ is free.
```
GS1=generalizedSplines(E1,I1);
GS2=generalizedSplines(E2,I2);
prune GS1 --this one is not free
prune GS2 --this one is free
```
